apply plugin: 'com.android.application'

def props = new Properties()
file("ad.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.alexchurkin.truckremote"
        minSdkVersion 17
        targetSdkVersion 32
        versionCode 34
        versionName "1.22"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'ADMOB_APP_ID', "\"" + props.getProperty("admobAppId") + "\"")
        manifestPlaceholders = [admobAppId:props.getProperty("admobAppId")]

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'

        multiDexEnabled true
    }
    buildTypes {

        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
            buildConfigField('String', 'INTERSTITIAL_AD_ID', "\"ca-app-pub-3940256099942544/1033173712\"")
            buildConfigField "boolean", "USE_LOG", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true

            //TODO УДАЛИТЬ ПОТОМ!!!
            applicationIdSuffix '.fix'
            versionNameSuffix ' (Fix)'

            buildConfigField('String', 'INTERSTITIAL_AD_ID', "\"" + props.getProperty("interstitialAdId") + "\"")
            buildConfigField "boolean", "USE_LOG", "false"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

android.defaultConfig.vectorDrawables.useSupportLibrary = true

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.multidex:multidex:2.0.1"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.5.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.google.android.gms:play-services-ads:20.6.0'
    implementation 'com.android.billingclient:billing:4.1.0'

    //Required to avoid crash on Android 12 API 31
    implementation 'androidx.work:work-runtime:2.7.1'
}